!                       *******************
                        SUBROUTINE INIT_KIN  
!                       *******************
! 
     * (W,FLUSCE,NUBO,VNOIN,AT,DT,LT,NIT,
     *  NELEM,NSEG,NPTFR,FLUX,AIRS,AIRE,
     *  X,Y,IKLE,ZF,CF,NPOIN,HN,H,U,V,QU,QV,G,LISTIN,
     *  XNEBOR,YNEBOR,XSGBOR,YSGBOR,LIMPRO,NBOR,KDIR,KNEU,KDDL, 
     *  HBOR,UBOR,VBOR,FLUSORT,FLUENT,CFLWTD,DTVARI,NELMAX,KFROT,  
     *  NREJET,ISCE,TSCE2,MAXSCE,MAXTRA,YASMH,SMH,MASSES,
     *  NTRAC,DIMT,T,HTN,TN,DLIMT,LIMTRA,
     *  TBOR,MASSOU,FLUTENT,FLUTSOR,DTHAUT,DPX,DPY,DJX,DJY,CMI,JMI,
     *  SMTR,DXT,DYT,DJXT,DJYT,DIFVIT,ITURB,PROPNU,DIFT,DIFNU,
     *  DX,DY,OPTVF,FLUSORTN,FLUENTN,
     *  DSZ,AIRST,HSTOK,HCSTOK,FLUXT,FLUHBOR,FLBOR,
     *  LOGFR,LTT,DTN,FLUXTEMP,FLUHBTEMP,
     *  HC,TMAX,DTT,T1,T2,T3,T4,T5,ICIN,EPS,NORDRE,IVIS)
!
!***********************************************************************
! TELEMAC2D   V6P1                                           29/03/2011
!***********************************************************************
!
!brief    1. INITILIZATION ROUTINE FOR KINETIC SCHEMES
!+
!+
!
!
!history  R. ATA (EDF-LNHE)
!+        03/15/2011
!+        V6P1
!
!
!~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
!| AIRE           |-->| ELEMENT AREA
!| AIRS           |-->| CELL AREA
!| AIRST          |-->| AREA OF SUB-TRIANGLES (SECOND ORDER)
!| AT,DT,LT       |-->| TIME, TIME STEP AND NUMBER OF THE STEP
!| CF             |-->| FRICTION COEFFICIENT
!| CFLWTD         |-->| WANTED CFL NUMBER
!| CMI            |-->| COORDINATES OF MIDDLE PONTS OF EDGES
!| DIFNU          |-->| COEFFICIENT OF DIFFUSION FOR TRACER
!| DIFT           |-->| LOGICAL: DIFFUSION FOR TRACER OR NOT
!| DIFVIT         |-->|  LOGICAL: DIFFUSION FOR VELOCITY OR NOT
!| DIMT           |-->| DIMENSION OF TRACER
!| DJXT,DJYT      |---| WORKING TABLES FOR TRACER
!| DLIMT          |-->| DIMENSION OF TRACER ON THE BOUNDARY
!| DSZ            |<->| VARIATION OF Z FOR ORDER 2
!| DTHAUT         |-->| CHARACTERISTIC LENGTH (DX) USED FOR CFL
!| DTN            |<->| TIME STEP   FROM TN+1 TO TN+2
!| DTT            |<->| TIME STEP FOR TRACER
!| DTVARI         |-->| DT VARIALE OR NOT
!| DX,DY          |---| WORKING TABLES
!| DXT,DYT        |---| WORKING TABLES FOR TRACER
!| EPS            |-->| THRESHOLD VALUE FOR DEPTH
!| FLUENT,FLUSORT |<--| MASS FLUX MASSE INLET AND OUTLET FROM TN TO TN+1
!| FLUHBTEMP      |<->| BOUNDARY FLUX FOR TRACER
!| FLUSCE         |-->| SOURCE FLUXES
!| FLUSORTN,FLUENT|<->| MASS FLUX MASSE INLET AND OUTLET FROM TN+1 TO TN+2
!| FLUTENT,FLUTSOR|<--| FLUX TRACER INLET AND OUTLET
!| FLUX           |---| FLUX
!| FLUXT,FLUHBOR  |<->| FLUX, FLUX BORD FOR TRACER
!| FLUXTEMP       |<->| FLUX POUR TRACER
!| G              |-->| GRAVITY
!| H              |<--| WATER DEPTH AT TIME N+1
!| HBOR           |-->| IMPOSED VALUE FOR H
!| HC             |<->| H RECONSTRUCTED (ORDER 2) CORRECTED
!| HN             |-->| WATER DEPTH AT TIME N
!| HSTOK,HCSTOK   |<->| H, H CORRECTED TO STOCK FOR TRACER
!| HTN,TN         |-->| HT, T  AT TIME N
!| IKLE           |-->| INDICES OF NODES FOR TRIANGLE
!| ISCE           |-->| SOURCE POINTS
!| ITURB          |-->| MODEL OF TURBULENCE  1 : LAMINAIRE
!| JMI            |-->| NUMBER OF THE TRIANGLE IN WHICH IS LOCATED 
!|                |   | THE MIDPOINT OF THE INTERFACE
!| KDDL           |-->| CONVENTION FOR FREE POINTS (BC)
!| KDIR           |-->| CONVENTION FOR DIRICHLET POINTS
!| KFROT          |-->| BED FRICTION LAW 
!| KNEU           |-->| CONVENTION NEUMANN POINTS
!| LIMPRO         |-->| TYPES OF BOUNDARY CONDITION
!| LIMTRA         |-->| TYPES OF BOUNDARY CONDITION FOR TRACER
!| LISTIN         |-->| IF YES, PRINT MESSAGES AT LISTING.
!| LOGFR          |<->| REFERENCE OF BOUNDARY NODES
!| LTT            |<->| NUMBER OF TIME STEP FOR TRACER
!| MASSES         |<--| ADDED MASS BY SOURCE TERMS
!| MASSOU         |<--| ADDED TRACER MASS BY SOURCE TERM
!| MAXSCE         |-->| MAXIMUM NUMBER OF SOURCES
!| MAXTRA         |-->| MAXIMUM NUMBER OF TRACERS
!| NBOR           |-->| GLOBAL INDICES FOR BORD NODES
!| NELEM          |-->| NUMBER OF ELEMENTS
!| NELMAX         |-->| MAXIMUM NUMBER OF ELEMENTS
!| NIT            |-->| TOTAL NUMBER OF TIME STEPS
!| NPOIN          |-->| TOTAL NUMBER OF NODES
!| NPTFR          |-->| TOTAL NUMBER OF BOUNDARY NODES
!| NREJET         |-->| NUMBER OF SOURCE/SINK
!| NSEG           |-->| NUMBER OF EDGES
!| NTRAC          |-->| NUMBER OF TRACERS
!| NUBO           |-->| GLOBAL INDICES OF EDGE EXTREMITIES
!| OPTVF          |-->| OPTION OF THE SCHEME
!|                |   | 0:ROE, 1:KINETIC ORDRE 1,2:KINETIC ORDRE 2
!|                |   | 3:ZOKAGOA, 4:TCHAMEN,4:HLLC
!| PROPNU         |-->| COEFFICIENT OF MOLECULAR DIFFUSION 
!| QU,QV          |<->| FLOW COMPOENENTS AT TIME N THEN AT TIME  N+1
!| SMH            |-->| SOURCE TERMS FOR CONTINUITY EQUATION
!| SMTR           |-->| SOURCE TERMS FOR TRACERS
!| T              |<--| TRACER UPDATED
!| T1,T2,T3,T4,T5 |---| WORKING TABLES
!| TBOR           |-->| BC FOR T
!| TMAX           |-->| FINAL TIME
!| TSCE2          |-->| PRESCRIBED VALUES OF TRACERS AT SOURCES
!| U,V            |<--| VELOCITY COMPONENTS AT TIME N+1
!| UBOR           |-->| IMPOSED VALUES FOR U
!| VBOR           |-->| IMPOSED VALUES FOR V
!| VNOIN          |-->| NORMAL TO THE INTERFACE
!|                |   | (2 FIRS COMPOSANTES) AND 
!|                |   | SEGMENT LENGTH (3RD COMPONENT)
!| W              |<->| WORKING TABLE
!| X,Y            |-->| COORDINATES FOR MESH NODES
!| XNEBOR,YNEBOR  |-->| NORMAL TO BOUNDARY POINTS
!| YASMH          |-->| LOGICAL: TO TAKE INTO ACCOUNT SMH
!| ZF             |-->| BED TOPOGRAPHY (BATHYMETRY)
!~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
!
      USE BIEF
      USE INTERFACE_TELEMAC2D, EX_INIT_KIN => INIT_KIN
!
      IMPLICIT NONE
!
      INTEGER LNG,LU                                                    
      COMMON/INFO/LNG,LU                                                
!
!+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+
!
      INTEGER, INTENT(IN) :: NELEM,NPOIN,NSEG,NPTFR,LT,NIT,NREJET,DIMT
      INTEGER, INTENT(IN) :: MAXSCE,MAXTRA,ICIN,NORDRE,IVIS
      INTEGER, INTENT(IN) :: DLIMT,OPTVF,JMI(*)
      INTEGER, INTENT(IN) :: KDIR,KNEU,KDDL,ITURB,NELMAX,KFROT,NTRAC
      INTEGER, INTENT(IN) :: NUBO(2,*),LIMPRO(NPTFR,6),NBOR(NPTFR)
      INTEGER, INTENT(IN) :: IKLE(NELMAX,3),ISCE(NREJET),LIMTRA(DLIMT)
      INTEGER, INTENT(INOUT) :: LTT,LOGFR(*)
! 
      LOGICAL, INTENT(IN) :: LISTIN,DTVARI,YASMH,DIFVIT,DIFT
      DOUBLE PRECISION, INTENT(INOUT) :: T1(*),T2(*),T3(*),T4(*),T5(*)
      DOUBLE PRECISION, INTENT(IN)    :: XNEBOR(2*NPTFR),YNEBOR(2*NPTFR)
      DOUBLE PRECISION, INTENT(IN)    :: XSGBOR(NPTFR,4),YSGBOR(NPTFR,4)	
      DOUBLE PRECISION, INTENT(INOUT) :: DT
      DOUBLE PRECISION, INTENT(IN)    :: EPS,AT,VNOIN(3,*)
      DOUBLE PRECISION, INTENT(IN)    :: TSCE2(MAXSCE,MAXTRA)   
      DOUBLE PRECISION, INTENT(INOUT) :: W(3,NPOIN),FLUSORTN,FLUENTN   
      DOUBLE PRECISION, INTENT(IN)    :: AIRE(NPOIN),DTHAUT(NPOIN)
      DOUBLE PRECISION, INTENT(IN)    :: HBOR(NPTFR),UBOR(NPTFR)
      DOUBLE PRECISION, INTENT(IN)    :: VBOR(NPTFR),HN(NPOIN)
      DOUBLE PRECISION, INTENT(IN)    :: SMH(NPOIN),ZF(NPOIN),CF(NPOIN)
      DOUBLE PRECISION, INTENT(INOUT) :: U(NPOIN),V(NPOIN)
      DOUBLE PRECISION, INTENT(IN)    :: H(NPOIN),QU(NPOIN),QV(NPOIN)
      DOUBLE PRECISION, INTENT(IN)    :: DPX(3,NELMAX),DPY(3,NELMAX)
      DOUBLE PRECISION, INTENT(IN)    :: X(NPOIN),Y(NPOIN),AIRS(NPOIN)
      DOUBLE PRECISION, INTENT(INOUT) :: FLUSCE(3,NPOIN),FLUX(NPOIN,3)
      DOUBLE PRECISION, INTENT(INOUT) :: FLUSORT,FLUENT,MASSES
      DOUBLE PRECISION, INTENT(INOUT) :: FLUTENT(*),FLUTSOR(*)
      DOUBLE PRECISION, INTENT(INOUT) :: MASSOU(*)
      DOUBLE PRECISION, INTENT(IN)    :: G,CFLWTD,AIRST(2,NSEG)
      DOUBLE PRECISION, INTENT(INOUT) :: HSTOK(*),HCSTOK(2,*),DTT
      DOUBLE PRECISION, INTENT(INOUT) :: CMI(2,NSEG)
      DOUBLE PRECISION, INTENT(IN)    :: PROPNU,DIFNU,TMAX
      DOUBLE PRECISION, INTENT(INOUT) :: DJX(3,NELMAX),DJY(3,NELMAX)
      DOUBLE PRECISION, INTENT(INOUT) :: DX(3,NPOIN),DY(3,NPOIN)
      DOUBLE PRECISION, INTENT(INOUT) :: DJXT(NELMAX),DJYT(NELMAX)
      DOUBLE PRECISION, INTENT(INOUT) :: DXT(NPOIN),DYT(NPOIN)
      DOUBLE PRECISION, INTENT(INOUT) :: DSZ(2,NSEG)
      DOUBLE PRECISION, INTENT(INOUT) :: HC(2,NSEG),DTN 
!
      TYPE(BIEF_OBJ) , INTENT(IN)     :: TBOR,TN
      TYPE(BIEF_OBJ) , INTENT(INOUT)  :: T,HTN,SMTR,FLUHBOR,FLUHBTEMP
      TYPE(BIEF_OBJ) , INTENT(INOUT)  :: FLUXTEMP,FLUXT,FLBOR         
!
!+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+
!
      INTEGER I,IS,K,ITRAC
!
      DOUBLE PRECISION BETA
!
!-----------------------------------------------------------------------
!
      IF(LT.EQ.1) THEN
!
!       COMPUTE GRADIENT OF ZF FOR ORDRE2
!
        IF(NORDRE.EQ.2) THEN
          CALL GRADZ(NPOIN,NELMAX,NSEG,IKLE,NUBO,X,Y,AIRE,AIRS,CMI,JMI,
     *               ZF,DPX,DPY,DSZ,BETA,AIRST,T1,T2,T3,T4,T5)
        ENDIF
!
!       INITIALIZATION FOR TRACER
!
        IF(NTRAC.GT.0) THEN
          DO ITRAC=1,NTRAC
            MASSOU(ITRAC) = 0.D0
            FLUTENT(ITRAC)= 0.D0
            FLUTSOR(ITRAC)= 0.D0
            DO IS=1,NPOIN
              HTN%ADR(ITRAC)%P%R(IS) = HN(IS) * TN%ADR(ITRAC)%P%R(IS)
            ENDDO
            CALL OS('X=Y     ',X=T%ADR(ITRAC)%P,Y=TN%ADR(ITRAC)%P)
          ENDDO
        ENDIF
!
!       DEFINITION OF A REFERENCE TO DISTINGUISH INTERIOR
!       AND BOUNDARY NODES FOR TRACER ORDRE 2
! 
        DO IS=1,NPOIN
          LOGFR(IS)=0
        ENDDO
!
        DO K=1,NPTFR
          IS=NBOR(K)
          IF(LIMPRO(K,2).EQ.KDIR) LOGFR(IS)=1
          IF(LIMPRO(K,1).EQ.KDIR) LOGFR(IS)=3
          IF(LIMPRO(K,1).EQ.KNEU) LOGFR(IS)=2      
        ENDDO
!
      ENDIF
! 
!-----------------------------------------------------------------------
!
      IF(LT.EQ.1.OR.NTRAC.EQ.0) THEN
!
!     VARIABLES INTO W
!
      DO I=1,NPOIN
        W(1,I)= HN(I) 
        IF(HN(I).GT.EPS) THEN
          W(2,I) = QU(I) / HN(I)
          W(3,I) = QV(I) / HN(I)
        ELSE
          W(2,I) = 0.D0
          W(3,I) = 0.D0
        ENDIF
      ENDDO
!
!     COMPUTE TIME STEP UNDER CFL CONDITION (ORDRE 1)
!
      CALL CALDT(NPOIN,G,HN,U,V,DTHAUT,DTN,CFLWTD,ICIN,DTVARI,LISTIN)
!
!     COMPUTE HYDRO FLUXES
! 
      CALL FLUHYD(NPOIN,NELMAX,NSEG,NPTFR,NUBO,G,DTN,X,Y,AIRS,IKLE,AIRE,
     *            W,ZF,VNOIN,FLUX,NBOR,LIMPRO,XNEBOR,YNEBOR,KDIR,KNEU,
     *            KDDL,HBOR,UBOR,VBOR,FLUENTN,FLUSORTN,NORDRE,CMI,JMI,
     *            DJX,DJY,DX,DY,DTHAUT,CFLWTD,FLBOR,DPX,DPY,IVIS,
     *            PROPNU,FLUHBTEMP,BETA,DSZ,AIRST,HC,FLUXTEMP,NTRAC)
!
      IF(NTRAC.GT.0) THEN
!       INITIALIZATION FOR TRACER
        CALL REINIT(NPOIN,NSEG,NPTFR,HN,
     *              SMTR,HSTOK,HC,HCSTOK,FLUXT,FLUHBOR,DTT,NTRAC)
      ENDIF
!
      ENDIF
! 
!-----------------------------------------------------------------------
!
      RETURN
      END
